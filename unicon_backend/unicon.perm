entity user {}

entity role {
    relation assignee @user
}

entity organisation {
    relation owner @user

    relation admin @user
    relation observer @user

    action edit_roles = owner
    action delete = owner
    action edit = delete or admin
    action view = edit or observer
} 

entity project {
    relation org @organisation
    relation member @user
    
    // assignable to roles
    relation view_problems_access @role#assignee
    relation view_full_problem_details_access @role#assignee
    relation create_problems_access @role#assignee
    relation edit_problems_access @role#assignee
    relation delete_problems_access @role#assignee

    relation view_restricted_problems_access @role#assignee
    relation view_full_restricted_problem_details_access @role#assignee // Includes test case details
    relation edit_restricted_problems_access @role#assignee
    relation delete_restricted_problems_access @role#assignee

    relation make_submission_access @role#assignee
    relation view_own_submission_access @role#assignee
    relation view_supervised_submission_access @role#assignee
    relation view_others_submission_access @role#assignee

    relation view_groups_access @role#assignee
    relation create_groups_access @role#assignee
    relation edit_groups_access @role#assignee
    relation delete_groups_access @role#assignee

    // actions from above roles (to combine with org permissions)
    action create_problems = org.edit or create_problems_access
    
    action view = view_restricted_problems or view_unrestricted_problems

    action view_restricted_problems = org.view or view_restricted_problems_access
    action view_unrestricted_problems = view_restricted_problems or view_problems_access 
    action view_full_restricted_problem_details = org.view or view_full_restricted_problem_details_access
    action view_full_unrestricted_problem_details = view_restricted_problems or view_full_problem_details_access
    action edit_restricted_problems = org.edit or edit_restricted_problems_access
    action edit_unrestricted_problems = edit_restricted_problems or edit_problems_access
    action delete_restricted_problems = org.edit or delete_restricted_problems_access
    action delete_unrestricted_problems = delete_restricted_problems or delete_problems_access

    action make_submission = org.edit or make_submission_access
    action view_others_submission = org.view or view_others_submission_access
    action view_supervised_submission = org.view or view_supervised_submission_access
    action view_own_submission = view_others_submission or view_own_submission_access

    action view_roles = org.view
    action edit_roles = org.edit
    action add_roles = edit_roles
    action delete_roles = edit_roles

    action view_groups = org.view or view_groups_access
    action create_groups = org.edit or create_groups_access
    action edit_groups = org.edit or edit_groups_access
    action delete_groups = org.edit or delete_groups_access
}


entity problem {
    relation project @project

    // for special problems (e.g. exam problems)
    attribute restricted boolean

    // Isoformat strings
    attribute started_at string
    attribute closed_at string

    // Problems to show in index page. Does not include any question details.
    action view_index = (project.view_restricted_problems) or (project.view_unrestricted_problems not restricted)
    action view_hidden_details = (project.view_full_restricted_problem_details) or (project.view_full_unrestricted_problem_details not restricted)
    // Problems that can be viewed in more detail. Problem must be open or user has privilege to view hidden details.
    action view = view_index and (is_problem_open(started_at) or view_hidden_details)
     
    action edit = (project.edit_restricted_problems) or (project.edit_unrestricted_problems not restricted)
    action delete = (project.delete_restricted_problems) or (project.delete_unrestricted_problems not restricted)

    permission make_submission = ((view and in_submission_date_range(started_at, closed_at)) or edit) and project.make_submission 
    permission view_own_submission = view and project.view_own_submission
    permission view_others_submission = view and project.view_others_submission
    permission view_supervised_submission = view and project.view_supervised_submission
}

rule is_problem_open(started_at string) {
    started_at <= context.data.current_date
}

rule in_submission_date_range(started_at string, closed_at string) {
    started_at <= context.data.current_date && context.data.current_date <= closed_at
}

entity group {
    relation project @project

    relation member @user
    relation supervisor @user

    action view = project.view_groups
    action edit = project.edit_groups
    action delete = project.delete_groups
}

entity submission {
    relation problem @problem
 
    relation group @group // not related to a submission by a group, this is for the supervisor relationship
    relation owner @user
    
    action view = (owner and problem.view_own_submission) or (group.supervisor and problem.view_supervised_submission) or (problem.view_others_submission not owner)
}